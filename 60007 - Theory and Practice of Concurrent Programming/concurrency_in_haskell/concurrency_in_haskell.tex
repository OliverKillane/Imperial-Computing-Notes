\chapter{Concurrency in Haskell}
Haskell is an exemplar of the functional programming paradigm
\begin{itemize}
	\item Rich type system with powerful type inference, higher kinded-types, pattern matching, generalized algebraic datatypes, etc.
	\item Imperative programming can be performed by binding functions operating on monads (e.g IO), often using \mintinline{Haskell}{do} notation (syntactic sugar)
	\item Haskell can be easily made parallel with no changes to code (using runtime options for enabling SMP parallelism)
\end{itemize}
We are interested in concurrency (specifically threads communicating via shared resources).
\begin{minted}{Bash}
# threaded allows multiple cores to be used
ghc Program.hs -threaded

# We can also optimise with -O flags 
ghc -O2 Program.hs -threaded

# Run, informing the haskell runtime system (RTS) to schedule on 8 threads
./Program args +RTS -N8
\end{minted}

\section{Managing Threads}
\begin{minted}{Haskell}
import Control.Concurrent

{- A data type for a handle of a thread, it also represents a pointer to the 
  thread and the thread cannot be garbage collected until this pointer is 
  dropped. 
-}
data ThreadId

{- Takes in a computation (here in the IO monad) and returns a ThreadID (wrapped 
   with IO).

   Note that the thread is run asynchronously (if main thread terminates, all 
   threads end).
-}
forkIO :: IO () -> IO ThreadId
\end{minted}
The threads created with \mintinline{Haskell}{forkIO} are user-level threads managed by the \textit{Haskell Runtime System}.
\begin{itemize}
	\item Many of these lightweight thread can be scheduled across a smaller number of OS provided threads
	\item There is functionality provided in Haskell for creating OS managed threads (\mintinline{Haskell}{forkOS :: IO () -> IO ThreadId})
\end{itemize}

\begin{examplebox}{Thready Set Go!}
	Create a basic haskell program to print "hello world" on $n$ different threads.
	\tcblower
	\begin{minted}{Haskell}
import Control.Concurrent (MVar, takeMVar, newEmptyMVar, putMVar, forkIO)
import Control.Monad (replicateM, zipWithM)

printHello :: MVar () -> Integer -> IO ()
printHello h i = do
  -- print out the message
  putStrLn ("This is thread " ++ show i ++ " saying hello world!")  

  -- place a value in the mutable variable
  putMVar h ()

main :: IO ()
main = do
  -- create the mutable variables
  mvars <- replicateM 10 newEmptyMVar  

  -- for each mutable variable fork off a process with it & the index
  res <- zipWithM (\ x y -> forkIO (printHello x y)) mvars [1..]  

  -- for each mutable variable, attempt to get the value (will wait until a value is put)
  mapM_ takeMVar mvars  
    \end{minted}
\end{examplebox}

\begin{sidenotebox}{Map a monad}
	\begin{minted}{Haskell}
import Control.Monad

mapM  :: (Traversable t, Monad m) => (a -> m b) -> t a -> m (t b)
mapM_ :: (Foldable t, Monad m)    => (a -> m b) -> t a -> m ()
    \end{minted}
	We can use \mintinline{Haskell}{mapM} to bind together the application of some function to every item in some traversable structure. There are also functions for \mintinline{Haskell}{zipWithM}, \mintinline{Haskell}{foldM} etc.
	\\
	\\ The \mintinline{Haskell}{function_} variants discard the final value (equivalent to finally binding \mintinline{Haskell}{return ()}).
\end{sidenotebox}

\section{MVars}
An \mintinline{Haskell}{MVar} is a mutable variable.
\begin{minted}{Haskell}
import Control.Concurrent (MVar, newEmptyMVar, newMVar, takeMVar, putMVar)

-- Creates a new empty MVar
newEmptyMVar :: IO (MVar a)

-- Creates a new full MVar
newMVar :: a -> IO (MVar a)

-- Blocks until the MVar is full, then makes it empty
takeMVar :: MVar a -> IO a

-- Blocks until the mVar is empty, and then places a value (making the MVar full)
putMVar :: MVar a -> a -> IO ()
\end{minted}
\begin{itemize}
	\item Can be accessed by multiple threads.
	\item Contains another type (\mintinline{Haskell}{Int}, \mintinline{Haskell}{String}, \mintinline{Haskell}{Maybe Bool}, anything of kind \mintinline{Haskell}{*})
	\item Is either empty, of full, we can create empty variables, put to empty variables \& take from empty variables.
	\item Taking from an empty variable will wait on the variable being made full, in this way we can wait for threads to create values, or to terminate.
	\item Internally \mintinline{Haskell}{MVar}s are backed by mutexes.
\end{itemize}

\begin{examplebox}{Join me!}
	Send a message from a thread to main, have main join on the result.
	\tcblower
	\begin{minted}{Haskell}
import Control.Concurrent ( forkIO, newEmptyMVar, putMVar, takeMVar, MVar )

childThread :: MVar String -> IO ()
childThread m = do
  let msg = "Hello notes reader!"
  putStrLn $ "Determining the message as: " ++ msg
  putMVar m msg

main :: IO ()
main = do
  msgVar <- newEmptyMVar
  forkIO $ childThread msgVar
  msg <- takeMVar msgVar
  putStrLn $ "Got the message: " ++ msg
    \end{minted}
\end{examplebox}

\section{Mutexes}
We can use the \mintinline{Haskell}{MVar ()} type as a mutex.
\begin{minted}{Haskell}

inlock :: MVar () -> IO a -> IO a
inlock mutex comp = do
    -- Wait until the mutex is empty, and then fill to acquire
    putMVar mutex ()

    -- do some computation
    res <- comp

    -- Release the mutex by emptying it
    takeMVar mutex

    return res
\end{minted}
We could also do this the other way around (with an empty MVar meaning the mutex is locked)

\section{Channels}

\begin{center}
	\includegraphics[width=.8\textwidth]{concurrency_in_haskell/images/channel.drawio.png}
\end{center}

We can represent this using \mintinline{Haskell}{MVar}s.


\begin{minted}{Haskell}
type Stream a = MVar (item a)
data Item   a = Item a (Stream a)

data Channel a = Channel (MVar (Stream a)) (MVar (Stream a))

{- Create a new Channel

readEnd -> emptyMVar -> writeEnd

-}
newChannel :: IO (Channel a)
newChannel = do
    hole     <- newEmptyMVar
    readEnd  <- newMVar hole
    writeEnd <- newMVar hole
    return (Channel readEnd writeEnd)

writeChannel :: Channel a -> a -> IO ()
writeChannel (Channel _ writeEnd) val = do
    -- oldHole -> writeEnd
    newHole <- newEmptyMVar
    oldHole <- takeMVar writeEnd

    -- oldHole -> val -> writeEnd
    putMVar oldHole (Item val newHole)
    putMVar writeEnd newHole

readChannel :: Channel a -> IO a
readChannel (Channel readEnd _) = do
    -- readEnd -> item -> tail
    stream        <- takeMVar readEnd
    Item val tail <- takeMVar stream

    -- readEnd -> tail
    putMVar readEnd tail

    return val
\end{minted}


\noindent \mintinline{haskell}{Control.Concurrent.Chan} contains a channel abstraction to use:
\begin{minted}{Haskell}
newChan   :: IO (Chan a)
writeChan :: Chan a -> a -> IO ()
readChan  :: Chan a -> IO a
dupChan   :: Chan a -> IO (Chan a)

-- and for converting to/from lists
getChanContents :: Chan a -> IO [a]
writeList2Chan :: Chan a -> [a] -> IO ()
\end{minted}

\section{Mutable References}
\mintinline{Haskell}{MVar}s are not well suited to fine-grained concurrency, hence we can use \mintinline{Haskell}{IORef} for atomic mutable access to references.
\begin{minted}{Haskell}
import Data.IORef
-- Create a new reference
newIORef :: a -> IO (IORef a)

-- read a value from an IORef
readIORef :: IORef a -> IO a

-- write a value into an IORef
writeIORef :: IORef a -> a -> IO ()

-- Modify the contents of an IORef with some function, 
-- the ' variant is strict
modifyIORef  :: IORef a -> (a -> a) -> IO ()
modifyIORef' :: IORef a -> (a -> a) -> IO ()

-- atomically modify the contents of an IORef (safe for multithreaded programs), 
-- the functions can return a second value (here of type b) which is useful for 
-- returning the old value, new value etc
-- the ' variant is strict
atomicModifyIORef  :: IORef a -> (a -> (a, b)) -> IO b
atomicModifyIORef' :: IORef a -> (a -> (a, b)) -> IO b
\end{minted}

\begin{sidenotebox}{Atomic Operations}
	We will use the \href{https://hackage.haskell.org/package/atomic-primops}{atomic-primops library} here instead of the GHC provided primitives.
	\begin{minted}{Bash}
# Assuming you use cabal for haskell packages
cabal install atomic-primops

# If building which ghc (rather than using it as a dependency of a cabal project)
ghc Program.hs -package atomic-primops
    \end{minted}
\end{sidenotebox}

\begin{minted}{Haskell}
data AtomicCounter
type CTicket = Int

-- we can destructure tickets
peekCTicket :: CTicket -> Int

-- Create a new counter
newCounter :: Int -> IO AtomicCounter

-- atomic operations
casCounter :: AtomicCounter -> CTicket -> Int -> IO (Bool, CTicket)
incrCounter :: Int -> AtomicCounter -> IO Int
incrCounter_ :: Int -> AtomicCounter -> IO ()

-- non atomic
readCounter :: AtomicCounter -> IO Int
readCounterForCAS :: AtomicCounter -> IO CTicket
writeCounter :: AtomicCounter -> Int -> IO ()
\end{minted}

\begin{examplebox}{Thread-Safe Increment}
	Develop a program to run many threads, each atomically incrementing a counter. Use IORefs but do not use the atomic primitives library.
	\tcblower
	\begin{minted}{Haskell}
import Control.Concurrent (MVar, takeMVar, newEmptyMVar, putMVar, forkIO)
import Control.Monad (replicateM, zipWithM_)
import Data.IORef ( IORef, atomicModifyIORef, newIORef, readIORef )

increment :: MVar () -> IORef Integer -> Integer -> IO ()
increment h i t = do
  (oldval, newval) <- atomicModifyIORef i (\v -> (v + 1, (v, v+1)))
  putStrLn $ "Thread " ++ show t ++ ": " ++ show oldval ++ " to " ++ show newval
  putMVar h ()

main :: IO ()
main = do
  mvars   <- replicateM 10 newEmptyMVar
  counter <- newIORef 0
  zipWithM_ (\ x y -> forkIO (increment x counter y)) mvars [1..]
  mapM_ takeMVar mvars
  finalValue <- readIORef counter
  putStrLn $ "Final value is: " ++ show finalValue    
    \end{minted}
\end{examplebox}
